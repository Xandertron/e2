@name light bridge
@inputs 
@outputs 
@persist P:entity Enabled H
#[
    Xandertron5000 - STEAM_0:1:42942061 - github.com/Xandertron
    prop bridge to walk across air, middle click to toggle it, right control to decend
]#
runOnTick(1)
if(first()){
    propSpawnUndo(0)
    P = propSpawn("models/hunter/tubes/circle2x2.mdl",1)
    H  = entity():pos():z()-2
    Enabled = 1 
    P:setAlpha(0)
    holoCreate(1)
    holoModel(1,"cylinder")
    holoScale(1,vec(6,6,0.5))
    holoDisableShading(1,1)
}
if(Enabled){
    Pos = (owner():pos()+(owner():vel()*tickInterval()*2))*vec(1,1,0)+vec(0,0,H)
    holoPos(1,Pos)
    holoColor(1,hsv2rgb(vec((curtime()*10)%360,1,1))*0.75)
    holoAng(1,ang(0,curtime()*25,0))
    P:setPos(Pos)
}
P:propFreeze(1)
if(owner():keyPressed("rcontrol")){   
    H--
}
if(changed(owner():keyPressed("mouse_middle"))&owner():keyPressed("mouse_middle")){
    Enabled = !Enabled
    if(Enabled){
        H = (owner():pos():z()+owner():vel():z()*tickInterval())-30
        holoAlpha(1,255)
        P:propDraw(1)
        P:propNotSolid(0)
    }
    else{
        holoAlpha(1,0)
        P:propDraw(0)
        P:propNotSolid(1)
    }
}
